(load "mk.scm")
(load "z3-driver.scm")
(load "sign-domain.scm")
(load "test-check.scm")

;;; abstract interpreter, inspired by the interpreter in:
;;;
;;; http://matt.might.net/articles/intro-static-analysis/

;; Tag values produced by the abstract interp, to allow the interp to
;; produce closures, booleans, etc, in addition to bit vectors
;; representing abstract integers.

(define lookupo
  (lambda (x env val)
    (conde
      ((fresh (rest)
         (== `((,x . ,val) . ,rest) env)))
      ((fresh (y v rest)
         (== `((,y . ,v) . ,rest) env)
         (symbolo y)
         (=/= y x)
         (lookupo x rest val))))))

(define evalo
  (lambda (expr env val)
    (conde
      [(symbolo expr) (lookupo expr env val)]
      
      [(numbero expr)
       (fresh (v)
         (s/declareo v)
         (s/z3-alphao expr v)
         (== `(num ,v) val))]

      [(fresh (e1 e2 v1 v2 v)
         (== `(+ ,e1 ,e2) expr)
         (s/declareo v1)
         (s/declareo v2)
         (s/declareo v)
         ;; is this the ideal ordering?
         (== `(num ,v) val)
         (s/z3-plus-tableo v1 v2 v)
         (evalo e1 env `(num ,v1))
         (evalo e2 env `(num ,v2)))]

      [(fresh (e1 e2 v1 v2 v)
         (== `(* ,e1 ,e2) expr)
         (s/declareo v1)
         (s/declareo v2)
         (s/declareo v)
         ;; is this the ideal ordering?
         (== `(num ,v) val)
         (s/z3-times-tableo v1 v2 v)
         (evalo e1 env `(num ,v1))
         (evalo e2 env `(num ,v2)))]

      ;;; hmmm---seems like there is a problem here:
      ;;; if we want to make zero? and if work separately,
      ;;; evalo may return a bit vector or a boolean.
      ;;; How would this work, given the need for s/declareo?
      [(fresh (e1 e2 e3 v1)
         (== `(ifzero ,e1 ,e2 ,e3) expr)
         (s/declareo v1)
         ;; is this the ideal ordering?
         (evalo e1 env `(num ,v1))
         (conde
           [(s/chas-zeroo v1)
            (evalo e2 env val)]
           [(conde
              [(s/chas-poso v1)]
              [(s/chas-nego v1)])
            (evalo e3 env val)]))]

      [(fresh (x e body v env^)
         (== `(let ((,x ,e)) ,body) expr)
         (symbolo x)
         (== `((,x . ,v) . ,env) env^)
         (evalo e env v)
         (evalo body env^ val))]
      
      )))


(test "declaro-1"
  (run* (q)
    (s/declareo q))
  '(_.0))

(test "declaro-2"
  (run* (q)
    (s/declareo q)
    (s/declareo q))
  '(_.0))

(test "declaro-3"
  (run* (q)
    (s/declareo q)
    (s/chas-poso q))
  '(bitvec-100
    bitvec-111
    bitvec-110
    bitvec-101))

#|
;;; Non-declarative behavior
;;; compare with declareo-3 -- just swapped order of goals
(test "declareo-4"
  (run* (q)
    (s/chas-poso q)
    (s/declareo q))
  '?)
Exception in call-z3: error in z3 out.smt > out.txt
Type (debug) to enter the debugger.
|#

(test "declareo-5"
  (run* (q)
    (fresh (x y)
      (== (list x y) q)
      (s/declareo x)
      (s/declareo y)
      (== x y)
      (s/chas-poso x)))
  '((bitvec-100 bitvec-100)
    (bitvec-111 bitvec-111)
    (bitvec-110 bitvec-110)
    (bitvec-101 bitvec-101)))

(test "declareo-7"
  (run* (q)
    (fresh (x y)
      (== (list x y) q)
      (s/declareo x)
      (s/declareo y)
      (s/chas-poso x)))
  '((bitvec-100 _.0)
    (bitvec-111 _.0)
    (bitvec-110 _.0)
    (bitvec-101 _.0)))

(test "declareo-8"
  (run* (q)
    (fresh (x y)
      (== (list x y) q)
      (s/declareo x)
      (s/declareo y)))
  '((_.0 _.1)))

(test "declareo-9"
  (run* (q)
    (fresh (x y)
      (== (list x y) q)
      (s/declareo x)
      (s/declareo y)
      (s/chas-poso y)))
  '((_.0 bitvec-100)
    (_.0 bitvec-111)
    (_.0 bitvec-110)
    (_.0 bitvec-101)))

(test "declareo-10"
  (run* (q)
    (fresh (x y)
      (== (list x y) q)
      (== x y)
      (s/declareo x)
      (s/declareo y)))
  '((_.0 _.0)))

(test "declareo-11"
  (run* (q)
    (fresh (x y)
      (== (list x y) q)
      (== x y)
      (s/declareo x)
      (s/declareo y)
      (s/chas-poso x)
      (s/chas-poso y)))
  '((bitvec-100 bitvec-100)
    (bitvec-111 bitvec-111)
    (bitvec-110 bitvec-110)
    (bitvec-101 bitvec-101)))

(test "declareo-12"
  (run* (q)
    (fresh (x y)
      (== (list x y) q)
      (== x y)
      (s/declareo x)
      (s/declareo y)
      (s/chas-poso x)))
  '((bitvec-100 bitvec-100)
    (bitvec-111 bitvec-111)
    (bitvec-110 bitvec-110)
    (bitvec-101 bitvec-101)))

(test "declareo-13"
  (run* (q)
    (fresh (x y)
      (== (list x y) q)
      (s/declareo x)
      (s/declareo y)
      (s/chas-poso x)))
  '((bitvec-100 _.0)
    (bitvec-111 _.0)
    (bitvec-110 _.0)
    (bitvec-101 _.0)))

(test "declareo-14"
  (run* (q)
    (fresh (x y)
      (== (list x y) q)
      (s/declareo x)
      (s/declareo y)
      (s/chas-poso x)
      (s/chas-poso y)))
  '((bitvec-100 bitvec-100)
    (bitvec-111 bitvec-111)
    (bitvec-111 bitvec-110)
    (bitvec-111 bitvec-100)
    (bitvec-111 bitvec-101)
    (bitvec-110 bitvec-110)
    (bitvec-110 bitvec-111)
    (bitvec-110 bitvec-100)
    (bitvec-110 bitvec-101)
    (bitvec-101 bitvec-111)
    (bitvec-101 bitvec-101)
    (bitvec-100 bitvec-101)
    (bitvec-100 bitvec-111)
    (bitvec-101 bitvec-110)
    (bitvec-100 bitvec-110)
    (bitvec-101 bitvec-100)))


(test "lookupo-1"
  (run 3 (q)
    (fresh (x env val v)
      (s/declareo v)
      (s/chas-poso v)
      (symbolo x)
      (== `(num ,v) val)
      (== (list x env val) q)
      (lookupo x env val)))
  '(((_.0 ((_.0 . (num bitvec-100)) . _.1) (num bitvec-100)) (sym _.0))
    ((_.0 ((_.0 . (num bitvec-111)) . _.1) (num bitvec-111)) (sym _.0))
    ((_.0 ((_.0 . (num bitvec-110)) . _.1) (num bitvec-110)) (sym _.0))))

(test "lookupo-2"
  (run* (q)
    (fresh (x env val v1 v2)
      (s/declareo v1)
      (s/chas-poso v1)
      (s/declareo v2)
      (s/chas-nego v2)
      (== `((x . (num ,v1)) (y . (num ,v2))) env)
      (== (list env val) q)
      (lookupo 'x env val)))
  '((((x . (num bitvec-100)) (y . (num bitvec-001))) (num bitvec-100))
    (((x . (num bitvec-111)) (y . (num bitvec-111))) (num bitvec-111))
    (((x . (num bitvec-111)) (y . (num bitvec-101))) (num bitvec-111))
    (((x . (num bitvec-111)) (y . (num bitvec-001))) (num bitvec-111))
    (((x . (num bitvec-111)) (y . (num bitvec-011))) (num bitvec-111))
    (((x . (num bitvec-110)) (y . (num bitvec-101))) (num bitvec-110))
    (((x . (num bitvec-110)) (y . (num bitvec-111))) (num bitvec-110))
    (((x . (num bitvec-110)) (y . (num bitvec-001))) (num bitvec-110))
    (((x . (num bitvec-110)) (y . (num bitvec-011))) (num bitvec-110))
    (((x . (num bitvec-101)) (y . (num bitvec-111))) (num bitvec-101))
    (((x . (num bitvec-101)) (y . (num bitvec-011))) (num bitvec-101))
    (((x . (num bitvec-100)) (y . (num bitvec-011))) (num bitvec-100))
    (((x . (num bitvec-100)) (y . (num bitvec-111))) (num bitvec-100))
    (((x . (num bitvec-101)) (y . (num bitvec-101))) (num bitvec-101))
    (((x . (num bitvec-100)) (y . (num bitvec-101))) (num bitvec-100))
    (((x . (num bitvec-101)) (y . (num bitvec-001))) (num bitvec-101))))

(test "lookupo-3"
  (run* (q)
    (fresh (x env val v1 v2)
      (s/declareo v1)
      (s/declareo v2)
      (s/chas-poso v1)
      (s/chas-nego v2)      
      (== `((x . (num ,v1)) (y . (num ,v2))) env)
      (== (list env val) q)
      (lookupo 'y env val)))
  '((((x . (num bitvec-100)) (y . (num bitvec-001))) (num bitvec-001))
    (((x . (num bitvec-111)) (y . (num bitvec-111))) (num bitvec-111))
    (((x . (num bitvec-111)) (y . (num bitvec-101))) (num bitvec-101))
    (((x . (num bitvec-111)) (y . (num bitvec-001))) (num bitvec-001))
    (((x . (num bitvec-111)) (y . (num bitvec-011))) (num bitvec-011))
    (((x . (num bitvec-110)) (y . (num bitvec-101))) (num bitvec-101))
    (((x . (num bitvec-110)) (y . (num bitvec-111))) (num bitvec-111))
    (((x . (num bitvec-110)) (y . (num bitvec-001))) (num bitvec-001))
    (((x . (num bitvec-110)) (y . (num bitvec-011))) (num bitvec-011))
    (((x . (num bitvec-101)) (y . (num bitvec-111))) (num bitvec-111))
    (((x . (num bitvec-101)) (y . (num bitvec-011))) (num bitvec-011))
    (((x . (num bitvec-100)) (y . (num bitvec-011))) (num bitvec-011))
    (((x . (num bitvec-100)) (y . (num bitvec-111))) (num bitvec-111))
    (((x . (num bitvec-101)) (y . (num bitvec-101))) (num bitvec-101))
    (((x . (num bitvec-100)) (y . (num bitvec-101))) (num bitvec-101))
    (((x . (num bitvec-101)) (y . (num bitvec-001))) (num bitvec-001))))

(test "evalo-0a"
  (run* (q)
    (fresh (expr val)
      (== '3 expr)
      (== (list expr val) q)
      (evalo expr '() val)))
  '((3 (num bitvec-100))))

(test "evalo-0b"
  (run 10 (q)
    (fresh (expr val)
      (numbero expr)
      (== (list expr val) q)
      (evalo expr '() val)))
  '((0 (num bitvec-010))
    (-1 (num bitvec-001))
    (1 (num bitvec-100))
    (2 (num bitvec-100))
    (-2 (num bitvec-001))
    (3 (num bitvec-100))
    (4 (num bitvec-100))
    (5 (num bitvec-100))
    (-3 (num bitvec-001))
    (6 (num bitvec-100))))

(test "evalo-1"
  (run 10 (q)
    (fresh (expr val)
      (== (list expr val) q)
      (evalo expr '() val)))
  '((0 (num bitvec-010))
    (-1 (num bitvec-001))
    (1 (num bitvec-100))
    (2 (num bitvec-100))
    (-2 (num bitvec-001))
    (3 (num bitvec-100))
    (4 (num bitvec-100))
    (5 (num bitvec-100))
    (-3 (num bitvec-001))
    (6 (num bitvec-100))))

(test "evalo-2a"
  (run* (q)
    (fresh (expr val)
      (== `(+ 3 4) expr)
      (== (list expr val) q)
      (evalo expr '() val)))
  '(((+ 3 4) (num bitvec-100))))

(test "evalo-2b"
  (run 20 (q)
    (fresh (expr val e1 e2)
      (== `(+ ,e1 ,e2) expr)
      (== (list expr val) q)
      (evalo expr '() val)))
  '(((+ 0 -1) (num bitvec-001))
    ((+ 1 -1) (num bitvec-111))
    ((+ 1 -2) (num bitvec-111))
    ((+ 1 -3) (num bitvec-111))
    ((+ 0 1) (num bitvec-100))
    ((+ 2 -4) (num bitvec-111))
    ((+ 1 1) (num bitvec-100))
    ((+ 1 2) (num bitvec-100))
    ((+ 1 -4) (num bitvec-111))
    ((+ 1 -5) (num bitvec-111))
    ((+ 1 -6) (num bitvec-111))
    ((+ -1 -6) (num bitvec-001))
    ((+ 2 -7) (num bitvec-111))
    ((+ -2 3) (num bitvec-111))
    ((+ -3 -6) (num bitvec-001))
    ((+ -2 -7) (num bitvec-001))
    ((+ -2 -8) (num bitvec-001))
    ((+ 3 -8) (num bitvec-111))
    ((+ -4 -6) (num bitvec-001))
    ((+ -5 4) (num bitvec-111))))

(test "evalo-2"
  (run 50 (q)
    (fresh (expr val op e1 e2)
      (== `(,op ,e1 ,e2) expr)
      (== (list expr val) q)
      (evalo expr '() val)))
  '(((+ 0 -1) (num bitvec-001))
    ((+ 1 -1) (num bitvec-111))
    ((+ 1 -2) (num bitvec-111))
    ((+ 1 -3) (num bitvec-111))
    ((+ 0 1) (num bitvec-100))
    ((+ 2 -4) (num bitvec-111))
    ((+ 1 1) (num bitvec-100))
    ((+ 1 2) (num bitvec-100))
    ((+ 1 -4) (num bitvec-111))
    ((+ 1 -5) (num bitvec-111))
    ((+ 1 -6) (num bitvec-111))
    ((+ -1 -6) (num bitvec-001))
    ((+ 2 -7) (num bitvec-111))
    ((+ -2 3) (num bitvec-111))
    ((+ -3 -6) (num bitvec-001))
    ((+ -2 -7) (num bitvec-001))
    ((+ -2 -8) (num bitvec-001))
    ((+ 3 -8) (num bitvec-111))
    ((+ -4 -6) (num bitvec-001))
    ((+ -5 4) (num bitvec-111))
    ((+ -6 -7) (num bitvec-001))
    ((+ 0 0) (num bitvec-010))
    ((+ -7 -9) (num bitvec-001))
    ((+ 4 -9) (num bitvec-111))
    ((+ -8 5) (num bitvec-111))
    ((+ -8 -8) (num bitvec-001))
    ((+ -3 -9) (num bitvec-001))
    ((+ -9 -9) (num bitvec-001))
    ((+ 5 -10) (num bitvec-111))
    ((+ 6 -11) (num bitvec-111))
    ((+ 1 -12) (num bitvec-111))
    ((+ 1 -13) (num bitvec-111))
    ((+ 6 0) (num bitvec-100))
    ((+ 7 0) (num bitvec-100))
    ((+ -10 -10) (num bitvec-001))
    ((+ 1 -14) (num bitvec-111))
    ((+ 1 -15) (num bitvec-111))
    ((+ -11 -14) (num bitvec-001))
    ((+ -12 6) (num bitvec-111))
    ((* 0 0) (num bitvec-010))
    ((+ 2 -16) (num bitvec-111))
    ((+ 0 -15) (num bitvec-001))
    ((* -1 -1) (num bitvec-100))
    ((+ 2 -17) (num bitvec-111))
    ((+ 0 -16) (num bitvec-001))
    ((* 1 1) (num bitvec-100))
    ((+ 0 -17) (num bitvec-001))
    ((+ 0 -18) (num bitvec-001))
    ((* 2 -2) (num bitvec-001))
    ((+ -12 -12) (num bitvec-001))))

(test "evalo-3"
  (run 10 (q)
    (fresh (expr env val e)
      (== `(let . ,e) expr)
      (== (list expr env val) q)
      (evalo expr env val)))
  '((((let ([_.0 0]) 0) _.1 (num bitvec-010)) (sym _.0))
    (((let ([_.0 -1]) 0) _.1 (num bitvec-010)) (sym _.0))
    (((let ([_.0 0]) _.0) _.1 (num bitvec-010)) (sym _.0))
    (((let ([_.0 1]) -1) _.1 (num bitvec-001)) (sym _.0))
    (((let ([_.0 1]) 0) _.1 (num bitvec-010)) (sym _.0))
    (((let ([_.0 -1]) _.0) _.1 (num bitvec-001)) (sym _.0))
    (((let ([_.0 1]) 1) _.1 (num bitvec-100)) (sym _.0))
    (((let ([_.0 2]) 2) _.1 (num bitvec-100)) (sym _.0))
    (((let ([_.0 1]) _.0) _.1 (num bitvec-100)) (sym _.0))
    (((let ([_.0 3]) 1) _.1 (num bitvec-100)) (sym _.0))))

(test "evalo-4"
  (run 15 (q)
    (fresh (expr env val id e)      
      (== `(let ([,id ,e]) x) expr)
      (== (list expr env val) q)
      (evalo expr env val)))
  '(((let ([x 0]) x) _.0 (num bitvec-010))
    ((let ([x -1]) x) _.0 (num bitvec-001))
    ((let ([x 1]) x) _.0 (num bitvec-100))
    ((let ([x 2]) x) _.0 (num bitvec-100))
    ((let ([x -2]) x) _.0 (num bitvec-001))
    ((let ([x 3]) x) _.0 (num bitvec-100))
    ((let ([x 4]) x) _.0 (num bitvec-100))
    ((let ([x 5]) x) _.0 (num bitvec-100))
    ((let ([x -3]) x) _.0 (num bitvec-001))
    ((let ([x 6]) x) _.0 (num bitvec-100))
    (((let ([_.0 0]) x) ((x . _.1) . _.2) _.1)
     (=/= ((_.0 x)))
     (sym _.0))
    ((let ([x 7]) x) _.0 (num bitvec-100))
    ((let ([x 8]) x) _.0 (num bitvec-100))
    (((let ([_.0 -1]) x) ((x . _.1) . _.2) _.1)
     (=/= ((_.0 x)))
     (sym _.0))
    ((let ([x 9]) x) _.0 (num bitvec-100))))

(test "evalo-5"
  (run 10 (q)
    (fresh (expr e1 e2 e3 val)
      (== `(ifzero ,e1 ,e2 ,e3) expr)
      (== (list expr val) q)
      (evalo expr '() val)))
  '(((ifzero 0 0 _.0) (num bitvec-010))
    ((ifzero 0 -1 _.0) (num bitvec-001))
    ((ifzero 0 -2 _.0) (num bitvec-001))
    ((ifzero 0 1 _.0) (num bitvec-100))
    ((ifzero 0 -3 _.0) (num bitvec-001))
    ((ifzero 0 -4 _.0) (num bitvec-001))
    ((ifzero 0 -5 _.0) (num bitvec-001))
    ((ifzero 0 2 _.0) (num bitvec-100))
    ((ifzero 0 -6 _.0) (num bitvec-001))
    ((ifzero 0 -7 _.0) (num bitvec-001))))

(test "evalo-6"
  (run 10 (q)
    (fresh (expr e1 e2 e3 val)
      (== '5 e1)
      (== `(ifzero ,e1 ,e2 ,e3) expr)
      (== (list expr val) q)
      (evalo expr '() val)))
  '(((ifzero 5 _.0 0) (num bitvec-010))
    ((ifzero 5 _.0 -1) (num bitvec-001))
    ((ifzero 5 _.0 1) (num bitvec-100))
    ((ifzero 5 _.0 -2) (num bitvec-001))
    ((ifzero 5 _.0 2) (num bitvec-100))
    ((ifzero 5 _.0 -3) (num bitvec-001))
    ((ifzero 5 _.0 -4) (num bitvec-001))
    ((ifzero 5 _.0 -5) (num bitvec-001))
    ((ifzero 5 _.0 -6) (num bitvec-001))
    ((ifzero 5 _.0 -7) (num bitvec-001))))
